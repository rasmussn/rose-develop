%{
#include <stdio.h>
#include <string.h>

#include "crsh.hpp"

void yyerror(const char *str)
{
  fprintf(stderr,"error: %s\n",str);
}

int yywrap()
{
  return 1;
}

int yylex();

%}

%error-verbose

%union {
  int                 m_num;
  char*               m_str;
  Crsh::EnvValue*     m_env;
  Environment*        m_environment;
  std::string*        m_arg;
  Arguments*          m_arguments;
  std::string*        m_annotation;                      
  Annotations*        m_annotations;
  InvocationDesc*     m_invoke;
}

%token DBCONNECT
%token DBCREATE
%token DBCLOSE
%token EQUAL
%token EXIT
%token EXPORT
%token MONITOR
%token LBLK
%token NOTE
%token RBLK
%token RUN
%token QUAL
%token <m_str> QUOTSTR
%token SHELL
%token SPECIMEN
%token <m_num> NUM
%token <m_str> SQLLITE
%token <m_str> STRING
%token VAR

%type <m_num>         num num_opt
%type <m_str>         filename ident string specimen testsuite_opt
%type <m_str>         simple_string quoted_string 
%type <m_env>         export
%type <m_environment> environ_opt
%type <m_arg>         argument
%type <m_arguments>   arguments_opt
%type <m_annotation>  annotation
%type <m_annotations> annotations_opt
%type <m_invoke>      test_invoke

%start crush

%%
num              : NUM                          { $$ = $1; }
                 ;

filename         : SQLLITE                      { $$ = $1; }
                 ;

ident            : STRING                       { $$ = $1; }
                 ;

simple_string    : STRING                       { $$ = $1; }
                 ;

quoted_string    : QUOTSTR                      { $$ = crsh().unquoteString($1); }
                 ;

string           : simple_string                { $$ = $1; }
                 | quoted_string                { $$ = $1; }
                 ;

export           : EXPORT ident EQUAL string    { $$ = crsh().envvar($2, $4); }

environ_opt      : /* empty */                  { $$ = crsh().environment();       }
                 | environ_opt export           { $$ = crsh().environment($1, $2); }
                 ;

argument         : string                       { $$ = crsh().arg($1); }
                 | num                          { $$ = crsh().arg($1); }
                 ;

arguments_opt    : /* empty */                  { $$ = crsh().args();       }
                 | arguments_opt argument       { $$ = crsh().args($1, $2); }
                 ;

specimen         : string                       { $$ = $1; }
                 ;

annotation       : NOTE string                  { $$ = crsh().annotation($2); }
                 ;
                 
annotations_opt  : /* empty */                  { $$ = crsh().annotations();       }
                 | annotations_opt annotation   { $$ = crsh().annotations($1, $2); } 
                 ;

test_invoke      : annotations_opt
                   specimen arguments_opt       { $$ = crsh().invoke($1, $2, $3); }
                 ;

test_cmd         : annotations_opt
                   ident QUAL ident
                   LBLK
                     environ_opt
                     test_invoke
                   RBLK                         { crsh().test($2, $4, $1, $6, $7); }
                 ;

testsuite_opt    : /* empty */                  { $$ = NULL; }
                 | ident QUAL                   { $$ = $1; }
                 ;

num_opt          : /* empty */                  { $$ = -1; }
                 | num                          { $$ = $1; }
                 ;

exectest_cmd     : annotations_opt
                   testsuite_opt
                   RUN num_opt                  { crsh().runTest($1, $2, $4); }
                 ;
                 
monitor_cmd      : MONITOR EQUAL string         { crsh().setMonitor($3); }
                 ; 

shell_cmd        : SHELL test_invoke            { crsh().execute($2); }

dbconnect_cmd    : annotations_opt
                   DBCONNECT filename           { crsh().connectdb($1, $3); }
                 ;

dbcreate_cmd     : DBCREATE filename            { crsh().createdb($2); }
                 | DBCREATE filename QUAL ident { crsh().createdb($2, $4); }
                 ;

dbclose_cmd      : DBCLOSE                      { crsh().closedb(); }
                 ;

exit_cmd         : EXIT
                 ;

cmd              : test_cmd
                 | exectest_cmd
                 | shell_cmd
                 | monitor_cmd
                 | dbconnect_cmd
                 | dbcreate_cmd
                 | dbclose_cmd
                 | exit_cmd
                 ;

cmds_opt         : /* empty */
                 | cmd cmds_opt
                 ;

crush            : cmds_opt
                 ;
